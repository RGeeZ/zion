global

link in "rt_str.o"

link def __wcs__(x int_t) *wchar_t to __wcs_int
link def __wcs__(x int_t, y int_t) *wchar_t to __wcs_int_radix
link def __wcs__(x float_t) *wchar_t to __wcs_float
link def __wcs__(x *char_t) *wchar_t to __wcs_str
link def concat(x *wchar_t, y *wchar_t) *wchar_t to __wcs_concat

#
# str
#
# the str type represents unicode strings.
#

type str has
	var raw *wchar_t

var __empty_str str = str(mbstowcs(""r))

def __init__() str
	return __empty_str

def __finalize__(s str)
	posix.free(s.raw)

def __box__(s *char_t) str
	return str(mbstowcs(s))

def len(s str) int
	return int(posix.wcslen(s.raw))

def str(s str) str
	return str(posix.wcsdup(s.raw))

def str(s utf8) str
	return str(mbstowcs(s.raw))

def str(x int) str
	return str(__wcs__(x.raw))

def str(i int_t) str
	return str(__wcs__(i))

def str(x float) str
	return str(__wcs__(x.raw))

def str(x float_t) str
	return str(__wcs__(x))

def __plus__(x str, y str) str
	return str(concat(x.raw, y.raw))

def __plus__(x str, y int) str
	return str(concat(x.raw, str(y).raw))

def __plus__(x str, y int_t) str
	let y_expanded = __wcs__(y)
	let result = str(concat(x.raw, y_expanded))
	posix.free(y_expanded)
	return result

def __plus__(x str, y float) str
	let sy = __wcs__(y.raw)
	let ret = str(concat(x.raw, sy))
	posix.free(sy)
	return ret

def __eq__(x str, y str) bool
	return posix.wcscmp(x.raw, y.raw) == 0r ? true : false

def __ineq__(x str, y str) bool
	return posix.wcscmp(x.raw, y.raw) != 0r ? true : false

def str(buffer *char_t, length size_t) str
	# Create a managed string from a direct memory access
	# TODO: optimize this to avoid the malloc
	let retstr = posix.malloc(length + 1r as size_t) as *char_t
	posix.memcpy(retstr as *int8_t, buffer as *int8_t, length)
	retstr[length] = 0r as char_t
	let s = str(mbstowcs(retstr))
	posix.free(retstr);
	return s

def str(src *wchar_t, char_count int_t) str
	assert(char_count >= 0r)
	if char_count <= 0r
		return __empty_str
	let cb = sizeof(wchar_t) * (char_count + 1r)
	let dst = posix.malloc(cb) as *wchar_t
	posix.memcpy(dst, src, cb - sizeof(wchar_t))
	dst[char_count] = 0r
	return str(dst)

def strip(s str) str
	var start int_t = 0r
	let max int_t = posix.wcslen(s.raw)
	var lim int_t = max - 1r
	while start <= lim
		if not posix.iswspace(s.raw[start])
			break
		start += 1r
	
	while lim > start and posix.iswspace(s.raw[lim])
		lim -= 1r
	
	if start == 0r and lim == max - 1r
		return s
	
	# TODO: PERF: maybe allow slicing strings
	return str(&s.raw[start], lim - start + 1r)
