module _
link module posix
link module math

tag false
tag true

type bool is
	true or
	false

type TypeID has
	var id __typeid__

def assert(condition bool)
	if not condition
		print("assertion failed!")

def typeid(t any) TypeID
	return TypeID(__get_typeid__(t))

def __ineq__(x TypeID, y TypeID) bool
	return bool(x.id != y.id)

def __bool__(b bool) __bool__
	return __get_typeid__(b) == __get_typeid__(true)

def __bool__(x int) __bool__
	return __bool__(x != 0)

def __not__(b bool) bool
	return bool(__get_typeid__(b) != __get_typeid__(true))

def __not__(x int) bool
	return x == 0

def bool(x __bool__) bool
	if x
		return true
	else
		return false

type str has
	var raw __str__

def str(s str) str
	return s

def str(x bool) str
	if x
		return "true"
	else
		return "false"

def int(s str) int
	return int(__int__(s.raw))

type int has
	var raw __int__

def int(x int) int
	return x

def str(x int) str
	return str(__str__(x.raw))

def str(i __int__) str
	return str(__str__(i))

type float has
	var raw __float__

def float(x float) float
	return x

def str(x float) str
	return str(__str__(x.raw))

def str(x __float__) str
	return str(__str__(x))

def mask(x int, y int) int
	return int(__mask__(x.raw, y.raw))

def __mod__(x int, y int) int
	return int(x.raw % y.raw)

def __plus__(x str, y str) str
	return str(x.raw + y.raw)

def __plus__(x str, y int) str
	return str(x.raw + str(y).raw)

def __plus__(x str, y float) str
	return str(x.raw + str(y).raw)

def __plus__(x float, y float) float
	return float(x.raw + y.raw)

def __plus__(x float, y int) float
	return float(x.raw + y.raw)

def __plus__(x int, y float) float
	return float(x.raw + y.raw)

def __plus__(x int, y int) int
	return int(x.raw + y.raw)

def __minus__(x int, y int) int
	return int(x.raw - y.raw)

def __minus__(x float, y int) float
	return float(x.raw - __float__(y.raw))

def __minus__(x int, y float) float
	return float(__float__(x.raw) - y.raw)

def __minus__(x float, y float) float
	return float(x.raw - y.raw)

def __positive__(x int) int
	return x

def __positive__(x float) float
	return x

def __negative__(x int) int
	return int(-x.raw)

def __negative__(x float) float
	return float(-x.raw)

def __divide__(x int, y int) int
	return int(x.raw / y.raw)

def __divide__(x float, y int) float
	return float(x.raw / __float__(y.raw))

def __divide__(x int, y float) float
	return float(__float__(x.raw) / y.raw)

def __divide__(x float, y float) float
	return float(x.raw / y.raw)

def __times__(x int, y int) int
	return int(x.raw * y.raw)

def __times__(x float, y int) float
	return float(x.raw * __float__(y.raw))

def __times__(x int, y float) float
	return float(__float__(x.raw) * y.raw)

def __times__(x float, y float) float
	return float(x.raw * y.raw)

def __eq__(x any, y nil) __bool__
	return __isnil(x as __var_ref)

def __eq__(y nil, x any) __bool__
	return __isnil(x as __var_ref)

def __eq__(x int, y int) bool
	return bool(x.raw == y.raw)

def __eq__(x int, y float) bool
	return bool(__float__(x.raw) == y.raw)

def __eq__(x float, y int) bool
	return bool(x.raw == __float__(y.raw))

def __eq__(x float, y float) bool
	return bool(x.raw == y.raw)

def __eq__(x str, y str) bool
	return bool(x.raw == y.raw)

def __ineq__(x any, y nil) bool
	# TODO: optimize
	return not bool(__isnil(x as __var_ref))

def __ineq__(y nil, x any) bool
	# TODO: optimize
	return not bool(__isnil(x as __var_ref))

def __ineq__(x int, y int) bool
	return bool(x.raw != y.raw)

def __ineq__(x int, y float) bool
	return bool(__float__(x.raw) != y.raw)

def __ineq__(x float, y int) bool
	return bool(x.raw != __float__(y.raw))

def __ineq__(x float, y float) bool
	return bool(x.raw != y.raw)

def __lt__(x int, y int) bool
	return bool(x.raw < y.raw)

def __lte__(x int, y int) bool
	return bool(x.raw <= y.raw)

def __gt__(x int, y int) bool
	return bool(x.raw > y.raw)

def __gte__(x int, y int) bool
	return bool(x.raw >= y.raw)

def __lt__(x int, y float) bool
	return bool(__float__(x.raw) < y.raw)

def __lte__(x int, y float) bool
	return bool(__float__(x.raw) <= y.raw)

def __gt__(x int, y float) bool
	return bool(__float__(x.raw) > y.raw)

def __gte__(x int, y float) bool
	return bool(__float__(x.raw) >= y.raw)

def __lt__(x float, y int) bool
	return bool(x.raw < __float__(y.raw))

def __lte__(x float, y int) bool
	return bool(x.raw < __float__(y.raw))

def __gt__(x float, y int) bool
	return bool(x.raw > __float__(y.raw))

def __gte__(x float, y int) bool
	return bool(x.raw >= __float__(y.raw))

def print(x any) void
	posix.puts(str(x).raw)

type List{T} has
	var value T
	var next List{T}?

def nth(list List{any T}?, index int) any T?
	if list
		if index == 0
			return list.value
		else
			return nth(list.next, index - 1)

	return nil

def len(list List{any T}?) int
	if list
		return len(list.next) + 1
	return 0

def conj(maybe_list List{any T}?, item any T) List{any T}
	if list := maybe_list
		return List(item, list)
	return List(item, nil)


def str(list List{any T}?) str
	s := "["
	var seen_one __bool__ = __false__
	while __true__
		if node := list
			if seen_one
				s += ", "
			else
				seen_one = __true__

			s += str(node.value)
			list = node.next
		else
			break

	return s + "]"
