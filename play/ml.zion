module _
# test: pass
# expect: Symbol

get sys

type Node is
	Symbol(s str, pos uint)
	Sequence(nodes [Node])

[global]
fn str(n Node) str
	match n
		Symbol(s, pos)
			return "Symbol('" + s + "', " + pos + ")"
		Sequence(nodes)
			return "[" + join(", ", nodes) + "]"

type ParseState has
	s str
	i uint

type Step has
	node Node
	ps ParseState

type Parser = fn (ps ParseState) Step?

fn parse_token(k str) Parser
	return fn (ps ParseState) Step?
		var start = ps.i
		while start < len(ps.s) and isspace(ps.s[start])
			start += 1
		var i = start
		while i < len(ps.s) and i-start < len(k) and k[i-start] == ps.s[i]
			i += 1
		# print("while matching for token '" + k + "' we are at position " + i + " and start = " + start)

		if start != i
			symbol := ps.s[start:i]
			if symbol == k
				return Step(Symbol(symbol, start), ParseState(ps.s, i))
		return null

fn parse_symbol(ps ParseState) Step?
	var start = ps.i
	while start < len(ps.s) and isspace(ps.s[start])
		start += 1
	var i = start
	while i < len(ps.s) and isalpha(ps.s[i])
		i += 1

	if start != i
		return Step(Symbol(ps.s[start:i], start), ParseState(ps.s, i))
	else
		return null

fn __bitwise_or__(p1 Parser, p2 Parser) Parser
	return fn (ps ParseState) Step?
		step := p1(ps)
		if step != null
			return step
		else
			return p2(ps)
			
fn __plus__(p1 Parser, p2 Parser) Parser
	return fn (ps ParseState) Step?
		step1 := p1(ps)
		if step1 != null
			step2 := p2(step1.ps)
			if step2 != null
				return Step(Sequence([step1.node, step2.node]), step2.ps)
		return null
			
fn __plus__(s str, p2 Parser) Parser
	return parse_token(s) + p2
			
fn __plus__(p1 Parser, s str) Parser
	return p1 + parse_token(s)
			
fn parse_kleene_star(parser Parser) Parser
	return fn (ps ParseState) Step?
		nodes := new [Node]
		while ps.i < len(ps.s)
			next_step := parser(ps)
			if next_step != null
				append(nodes, next_step.node)
				ps = next_step.ps
			else
				break
		return Step(Sequence(nodes), ps)

fn main()
	program := "fn f x = * x x"
	print(program)
	ps := ParseState(program, 0)

	parse_expr := (parse_token("*") | parse_token("+") | parse_token("-")) + parse_symbol + parse_symbol
	parse_parens := "(" + parse_expr + ")"
	parser := "fn" + parse_symbol + parse_kleene_star(parse_symbol) + "=" + parse_expr

	step := parser(ps)
	if step != null
		print(step.node)
		print(step.ps.i)
